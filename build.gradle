plugins {
    id 'java-library'
    id 'idea'
    id 'signing'
    id 'maven-publish'
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

group 'io.github.over-run'
archivesBaseName = 'glutils'
version = '0.8.0-SNAPSHOT'

test {
    useJUnitPlatform()
}

repositories {
    mavenCentral()
    maven { url 'https://maven.aliyun.com/repository/central' }
}

dependencies {
    api platform('org.lwjgl:lwjgl-bom:3.2.3')

    api 'org.lwjgl:lwjgl'
    api 'org.lwjgl:lwjgl-opengl'
    api 'org.lwjgl:lwjgl-stb'
    api 'org.joml:joml:1.10.1'

    testRuntimeOnly 'org.lwjgl:lwjgl::natives-windows'
    testRuntimeOnly 'org.lwjgl:lwjgl-glfw::natives-windows'
    testRuntimeOnly 'org.lwjgl:lwjgl-opengl::natives-windows'
    testRuntimeOnly 'org.lwjgl:lwjgl-stb::natives-windows'
    testImplementation 'org.lwjgl:lwjgl-glfw'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

jar {
    from 'LICENSE'
    manifest {
        attributes 'Specification-Title': 'GLUtils',
                'Specification-Vendor': 'OverRun Organization',
                'Specification-Version': '0',
                'Implementation-Title': 'GLUtils 0',
                'Implementation-Vendor': 'OverRun Organization',
                'Implementation-Version': archiveVersion
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set 'sources'
    from sourceSets.main.allSource
    from 'LICENSE'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set 'javadoc'
    from javadoc
    from 'LICENSE'
}

artifacts {
    archives javadocJar, sourcesJar
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing.publications {
    mavenJava(MavenPublication) {
        groupId = 'io.github.over-run'
        artifactId = 'glutils'
        version = project.version
        description = 'Utils for OpenGL for Java.'
        from components.java
        pom {
            name = 'GLUtils'
            description = 'Utils for OpenGL for Java.'
            url = 'https://github.com/Over-Run/GLUtils'
            licenses {
                license {
                    name = 'The MIT License'
                    url = 'https://raw.githubusercontent.com/Over-Run/GLUtils/pre-alpha/LICENSE'
                }
            }
            developers {
                developer {
                    id = 'squid233'
                    name = 'squid233'
                    email = '513508220@qq.com'
                }
            }
            scm {
                connection = 'https://github.com/Over-Run/GLUtils.git'
                developerConnection = 'https://github.com/Over-Run/GLUtils.git'
                url = 'https://github.com/Over-Run/GLUtils'
            }
        }
    }
}
// You should add 'OSSRH_USERNAME', 'OSSRH_PASSWORD', 'signing.keyId',
// 'signing.password' and 'signing.secretKeyRingFile'
// to GRADLE_USER_HOME/gradle.properties
publishing.repositories {
    maven {
        name = "OSSRH"
        credentials {
            username System.getenv("OSSRH_USERNAME")
            password System.getenv("OSSRH_PASSWORD")
        }
        url = version.endsWith('SNAPSHOT')
                ? 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
                : 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
    }
    maven {
        name = "GitHubPackages"
        url = "https://maven.pkg.github.com/Over-Run/GLUtils"
        credentials {
            username = System.getenv("GITHUB_ACTOR")
            password = System.getenv("GITHUB_TOKEN")
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

idea.module.inheritOutputDirs = true
