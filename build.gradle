plugins {
    id 'java-library'
    id 'idea'
    id 'signing'
    id 'maven-publish'
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_11

group projGroupId
archivesBaseName = projArtifactId
version = projVersion

repositories {
    mavenCentral()
    maven { url 'https://maven.aliyun.com/repository/central' }
    maven { url 'https://jitpack.io' }
    // temporary maven repositories
    maven { url 'https://s01.oss.sonatype.org/content/repositories/releases' }
    maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
    api platform('org.lwjgl:lwjgl-bom:3.3.0')

    api 'org.lwjgl:lwjgl'
    api 'org.lwjgl:lwjgl-glfw'
    api 'org.lwjgl:lwjgl-opengl'
    api 'org.lwjgl:lwjgl-stb'
    api 'org.lwjgl:lwjgl-assimp'
    api 'org.joml:joml:1.10.3'
    api 'io.github.over-run:common-utils:0.3.0'

    compileOnly 'org.jetbrains:annotations:23.0.0'

    testRuntimeOnly 'org.lwjgl:lwjgl::natives-windows'
    testRuntimeOnly 'org.lwjgl:lwjgl-glfw::natives-windows'
    testRuntimeOnly 'org.lwjgl:lwjgl-opengl::natives-windows'
    testRuntimeOnly 'org.lwjgl:lwjgl-stb::natives-windows'
    testRuntimeOnly 'org.lwjgl:lwjgl-assimp::natives-windows'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

jar {
    from 'LICENSE'
    manifest {
        attributes 'Specification-Title': projName,
                'Specification-Vendor': 'Overrun Organization',
                'Specification-Version': '2',
                'Implementation-Title': projName,
                'Implementation-Vendor': 'Overrun Organization',
                'Implementation-Version': archiveVersion
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set 'sources'
    from sourceSets.main.allSource, 'LICENSE', 'changelog.txt'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set 'javadoc'
    from javadoc, 'LICENSE', 'changelog.txt'
}

artifacts {
    archives javadocJar, sourcesJar
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    options {
        //-encoding utf-8 -charset utf-8 -windowtitle "GLUtils 1.6.0 Javadoc" -link https://docs.oracle.com/en/java/javase/11/docs/api/
        encoding 'UTF-8'
        charSet 'UTF-8'
        author true
        links 'https://docs.oracle.com/en/java/javase/11/docs/api/'
        title "GLUtils $projVersion Javadoc"
    }
}

publishing.publications {
    mavenJava(MavenPublication) {
        groupId = projGroupId
        artifactId = projArtifactId
        version = projVersion
        description = projDesc
        from components.java
        pom {
            name = projName
            description = projDesc
            url = projVcs
            licenses {
                license {
                    name = 'The MIT License'
                    url = 'https://raw.githubusercontent.com/Over-Run/GLUtils/2.x/LICENSE'
                }
            }
            developers {
                developer {
                    id = 'squid233'
                    name = 'squid233'
                    email = '513508220@qq.com'
                }
            }
            scm {
                connection = "${projVcs}.git"
                developerConnection = "${projVcs}.git"
                url = projVcs
            }
        }
    }
}
// You should add 'OSSRH_USERNAME' and 'OSSRH_PASSWORD'
// to environment path
// You should add 'signing.keyId', 'signing.password'
// and 'signing.secretKeyRingFile'
// to GRADLE_USER_HOME/gradle.properties
publishing.repositories {
    maven {
        name = "OSSRH"
        credentials {
            username System.getenv("OSSRH_USERNAME")
            password System.getenv("OSSRH_PASSWORD")
        }
        url = version.endsWith('-SNAPSHOT')
                ? 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
                : 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
    }
}

signing {
    sign publishing.publications.mavenJava
}

idea.module.inheritOutputDirs = true
